//该文件为发布密钥配置
//如果想要本地编译
//可以在最外层目录添加sonatype.gradle文件
//并定义sonatypeUsername和sonatypePassword
apply from: '../../sonatype.gradle'

apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    // 定义发布什么
    publications {
        mavenJava(MavenPublication) {
            // groupId = project.group
            // artifactId = project.name
            // version = project.version
            // groupId,artifactId,version，如果不定义，则会按照以上默认值执行
            from components.java
            //artifact makeJar
            artifact sourcesJar
            artifact javadocJar
            pom {
                // 构件名称
                // 区别于artifactId，可以理解为artifactName
                name = 'Concept Download'
                // 构件描述
                description = 'Build a download in a simple way'
                // 构件主页
                url = 'https://github.com/Linyuzai/concept'
                // 许可证名称和地址
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // 开发者信息
                developers {
                    developer {
                        name = '黑白法师'
                        email = '120718461@qq.com'
                    }
                }
                // 版本控制仓库地址
                scm {
                    url = 'https://github.com/Linyuzai/concept'
                    connection = 'scm:git:git://github.com/Linyuzai/concept.git'
                    developerConnection = 'scm:git:ssh://git@github.com:Linyuzai/concept.git'
                }
            }
        }
    }
    // 定义发布到哪里
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                // 这里就是之前在issues.sonatype.org注册的账号
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('Xdoclint:none', '-quiet')
}
